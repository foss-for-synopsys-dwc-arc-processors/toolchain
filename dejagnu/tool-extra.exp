# Copyright (C) 2016-2017 Synopsys Inc.

# This program is free software; you can redistribute it and/or modify it
# under the terms of the GNU General Public License as published by the Free
# Software Foundation; either version 3 of the License, or (at your option)
# any later version.

# This program is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
# FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for
# more details.

# You should have received a copy of the GNU General Public License along
# with this program.  If not, see <http://www.gnu.org/licenses/>.

# Use the multilib flags and check if we compile for the feature
# passed into ARG
proc check_target_arc { arg } {
    global target_alias
    global board
    global board_info

    if {[info exists board]} {
	set target_board $board
    } else {
	set target_board [target_info name]
    }

    if { [board_info $target_board exists compiler] } {
	set compiler [board_info $target_board compiler]
    } else {
	set compiler [find_gcc]
    }

    if { $compiler == "" } {
	return 0
    }

    regexp "/.* " $compiler compiler
    set compiler [string trimright $compiler " "]
    verbose "compiler is $compiler"

    if { [which $compiler] == 0 } {
	return 0
    }

    if { [board_info $target_board exists multilib_flags] } {
	set opts [board_info $target_board multilib_flags]
    } else {
	set opts ""
    }

    set result [eval exec $compiler $opts -dM -E - < /dev/null]
    verbose "Default defines : $result"
    return [regexp "$arg" "$result"]
}

# Compile files from $NSIM_LIB_DIR for the nSIM extension library
# for the floating point instructions.
proc compile_quarkse_em_nsim_apex { } {
    global env
    global tmpdir
    global HOSTCXX
    global HOSTCXXFLAGS
    global ARC_NSIM_APEX_DIR

    set dir $ARC_NSIM_APEX_DIR
    set files [glob -directory $dir *]
    foreach source $files {
	# File name without directory and expansion.
	set filename [file rootname [file tail $source]]
	set dest "$tmpdir/quarkse_em_$filename.so"
	set cxxflags "-O2 -fPIC -shared -rdynamic"
	if { [info exists HOSTCXXFLAGS] } {
	    set cxxflags "$cxxflags $HOSTCXXFLAGS"
	}
	set inc "-I$env(NSIM_HOME)/inc"
	if { [info exists HOSTCXX] && \
	    [string length [string trim $HOSTCXX]] != 0 } {
	    set cxx "$HOSTCXX"
	} else {
	    set cxx "g++"
	}
	set cmd "$cxx $inc $cxxflags -o $dest $source"
	remote_exec host [concat sh -c [list "$cmd 2>&1"]] "" "/dev/null" "ld.tmp"
    }
}

# Return 1 if we compile for ARC64
proc check_target_arc64 { } {
    return [check_target_arc "__ARC64__"]
}

# Return 1 if we compile for ARCv2 HS
proc check_target_archs { } {
    return [check_target_arc "__ARCHS__"]
}

# Return 1 if we compile for ARCv2 EM
proc check_target_arcem { } {
    return [check_target_arc "__ARCEM__"]
}

# Return 1 if we compile for ARC700
proc check_target_arc700 { } {
    return [check_target_arc "__ARC700__"]
}

# Return 1 if we compile for ARC6xx
proc check_target_arc6xx { } {
    if { [check_target_arc "__ARC600__"]
	 || [check_target_arc "__ARC601__"] } {
	return 1
    }
    return 0
}

# Return 1 if we compile for QuarkSE ARC EM
proc check_target_quarkse_em { } {
    return [check_target_arc "__ARC_FPX_QUARK__"]
}

# Return 1 if we compile for QuarkSE2 ARC EM
proc check_target_quarkse2_em { } {
    return [check_target_arc "__ARC_FPU_QUARK2__"]
}

# Return 1 if we compile for ARC600
proc check_target_arc600 { } {
    return [check_target_arc "__ARC600__"]
}

# Return 1 if we compile for ARC601
proc check_target_arc601 { } {
    return [check_target_arc "__ARC601__"]
}

# Fix multilib flags for compat.exp tests of GCC testsuite. This function
# removes all multilib flags from board info and appends them to testglues's
# flags. It is necessary because compat.exp uses alternate compiler which
# may not support GCC's multilib flags (they must be passed manually to each
# compiler). However testglue must be compiled with those multilib flags so
# this function also appends them to testglue's flags using wrap_compile_flags
# variable. This function must be called last to let check_target_arc
# function detect ARC core by multilib flags correctly.
proc fix_gcc_compat_multilib_flags { } {
    global board
    global board_info

    if { [info exists board] } {
	set target_board $board
    } else {
	set target_board [target_info name]
    }

    # Get multilib flags if they exist.
    if { [board_info $target_board exists multilib_flags] } {
	set multilib_flags [board_info $target_board multilib_flags]
    } else {
	set multilib_flags ""
    }

    # Set flags for testglue.
    set_board_info wrap_compile_flags "$multilib_flags"

    # Erase multilib flags to prevent errors when alternative compiler
    # is used in compat.exp tests of GCC testsuite.
    unset_board_info multilib_flags
    set_board_info multilib_flags ""

    # Tests from compat.exp are short but sometimes some failing tests may
    # lead to hanging of nSIM. So it is better to set a small time limit for
    # the simulator.
    unset_board_info sim_time_limit
    set_board_info sim_time_limit 30
}

# vim: noexpandtab sts=4 ts=8:
