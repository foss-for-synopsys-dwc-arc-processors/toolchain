# Copyright (C) 1997, 1998, 1999, 2000, 2001, 2002, 2003 Free Software
# Foundation, Inc.
#
# Copyright (C) 2013-2017 Synopsys, Inc.
#
# Contributor: Anton Kolesov <anton.kolesov@synopsys.com>
# Contributor: Claudiu Zissulescu <Claudiu.Zissulescu@synopsys.com>
#
# This program is free software; you can redistribute it and/or modify it
# under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program. If not, see <http://www.gnu.org/licenses/>.

# This is a list of toolchains that are supported on this board.
set_board_info target_install ${target_triplet}

# Load the generic configuration for this board. This will define a basic set
# of routines needed by the tool to communicate with the board.
load_generic_config "sim"

# basic-sim.exp is a basic description for the standard Cygnus simulator.
load_base_board_description "basic-sim"

# We need extra procedures to determine for which cpu we simulate.
search_and_load_file "library file" "tool-extra.exp" ${boards_dir}
search_and_load_file "library file" "arc-common.exp" ${boards_dir}

# Any multilib options are set in an environment variable.
process_multilib_options [arc_get_multilib_options]

set xldflags "-Wl,--defsym=__DEFAULT_HEAP_SIZE=256m \
    -Wl,--defsym=__DEFAULT_STACK_SIZE=1024m"

# Set common defaults
if ![info exists nsim_flags] {
    set nsim_flags {}
}
lappend nsim_flags \
	-on nsim_isa_enable_timer_0 \
	-on nsim_isa_enable_timer_1

# Disable memory and instruction exceptions - execution will halt on error,
# instead of generating exception that may not be properly handled.
lappend nsim_flags \
    -off invalid_instruction_interrupt \
    -off memory_exception_interrupt

set nsim_bin "$::env(NSIM_HOME)/bin/nsimdrv"

set_board_info sim_time_limit 300

# Detect if nSIM Turbo is available and libstdc++ is being tested.  Most of the
# Dejagnu tests do not benefit from Turbo mode, because they are extremely
# small, hence they finish before Turbo can kick in.  However there is two
# certain floating point tests in libstdc++ that take quite a lot of time to
# execute with soft-FP - around 15 minutes on Xeon@3.5GHz host.  Enabling Turbo
# reduces this time to mere 1 minute.
if { $tool == "libstdc++" } {
    spawn $nsim_bin -on nsim_fast
    expect {
	"Download failed: no file name specified" {
	    verbose "nSIM Turbo is available"
	    lappend nsim_flags -on nsim_fast
	}
	default {
	    # nSIM would spit out an error message if Turbo is not supported.
	    verbose "nSIM Turbo is not available, increasing timeout"
	    set_board_info sim_time_limit 1200
	}
    }
}

# Check if nsim_download_elf_sections option is required - whether nSIM version
# is > 2015.06. If revision.txt doesn't exist - this is a private verification
# build of nSIM, so assume that this is the latest version.
set nsim_version_path "$::env(NSIM_HOME)/revision.txt"
if [file exists $nsim_version_path] {
    set nsim_version_fid [open $nsim_version_path r]
    set nsim_version_text [read $nsim_version_fid]
    close $nsim_version_fid
    regexp "Version\\s+: (\\d+)\.(\\d+)" $nsim_version_text -> \
	nsim_version_year nsim_version_month
    if { $nsim_version_year > 2015 || \
	($nsim_version_year == 2015 && $nsim_version_month > 6) } {
	# Avoid performance penalty of useless initialization of heap and stack
	# sections.
	lappend nsim_flags "-on nsim_download_elf_sections"
    }
} else {
    lappend nsim_flags "-on nsim_download_elf_sections"
}

# Hostlink library support
if { [info exists env(ARC_HOSTLINK_LIBRARY)] } {
    set xldflags "$xldflags -Wl,--whole-archive $env(ARC_HOSTLINK_LIBRARY) \
        -Wl,--no-whole-archive"
} elseif { [info exists env(ARC_NSIM_HOSTLINK)] } {
    set_board_info arc,hostlink $env(ARC_NSIM_HOSTLINK)

    if { [board_info $board arc,hostlink] != "metaware" && \
	 [board_info $board arc,hostlink] != "nsim" } {
        perror "Unknown nSIM HOSTLINK type $env(ARC_NSIM_HOSTLINK)"
        exit 1
    }
} else {
    # Use nSIM GNU IO hostlink by default
    set_board_info arc,hostlink "nsim"
}

if { [board_info $board arc,hostlink] == "metaware" } {
    lappend nsim_flags -prop=nsim_hlink_gnu_io_ext=1
} elseif { [board_info $board arc,hostlink] == "nsim" } {
    lappend nsim_flags -prop=nsim_emt=1
}

# Big-endian?
if [ string match arceb-* $target_triplet ] {
    lappend nsim_flags -on nsim_isa_big_endian
}

# Select processor family. Also set nsim_isa_sat, which is mandatory for GCC.
if { [check_target_arc64_64] } {
    lappend nsim_flags \
	-p nsim_isa_family=arc64     \
	-p nsim_isa_div_rem_option=2 \
	-p nsim_isa_mpy_option=9     \
	-p nsim_isa_mpy64=1	     \
	-p nsim_isa_div64_option=1   \
	-p nsim_isa_has_fp=1	     \
	-p nsim_isa_fp_vec_option=1  \
	-p nsim_isa_fp_hp_option=1   \
	-p nsim_isa_fp_dp_option=1   \
	-p nsim_isa_fp_div_option=1  \
	-p nsim_isa_fp_num_regs=32   \
	-p nsim_isa_unaligned_option=1
} elseif { [check_target_arc64_32] } {
    lappend nsim_flags		     \
	-p nsim_isa_family=av3hs     \
	-p nsim_isa_pc_size=32	     \
	-p nsim_isa_addr_size=32     \
	-p nsim_isa_swap_option=1    \
	-p nsim_isa_bitscan_option=1 \
	-p nsim_isa_div_rem_option=2 \
	-p nsim_isa_mpy_option=9     \
	-p nsim_isa_unaligned_option=1
} elseif { [check_target_arc700] } {
    # GCC requires multiplier for ARC 700.
    lappend nsim_flags \
	-p nsim_isa_family=a700 \
	-on nsim_isa_sat \
	-on nsim_isa_mpy32
} elseif { [check_target_quarkse_em] } {
    compile_quarkse_em_nsim_apex
    lappend nsim_flags \
	-p nsim_isa_family=av2em \
	-p nsim_isa_core=3 \
	-p nsim_isa_mpy_option=3 \
	-p nsim_isa_div_rem_option=2 \
	-p nsim_isa_code_density_option=2 \
	-prop=nsim_ext=$tmpdir/quarkse_em_fscmp.so \
	-prop=nsim_ext=$tmpdir/quarkse_em_fsdiv.so \
	-prop=nsim_ext=$tmpdir/quarkse_em_fsflt2i.so \
	-prop=nsim_ext=$tmpdir/quarkse_em_fsi2flt.so \
	-prop=nsim_ext=$tmpdir/quarkse_em_fssqrt.so \
	-p nsim_isa_lpc_size=16 \
	-p nsim_isa_spfp=compact \
	-p nsim_isa_dpfp=compact
} elseif { [check_target_quarkse2_em] } {
    compile_quarkse_em_nsim_apex
    lappend nsim_flags \
	-p nsim_isa_family=av2em \
	-p nsim_isa_core=3 \
	-p nsim_isa_mpy_option=2 \
	-p nsim_isa_div_rem_option=2 \
	-p nsim_isa_code_density_option=2 \
	-prop=nsim_ext=$tmpdir/quarkse_em_fsdiv.so \
	-prop=nsim_ext=$tmpdir/quarkse_em_fssqrt.so
} elseif { [check_target_arc600] } {
    lappend nsim_flags \
	-p nsim_isa_family=a600 \
	-p nsim_isa_core=6 \
	-on nsim_isa_sat
} elseif { [check_target_arc601] } {
    lappend nsim_flags \
	-p nsim_isa_family=a601 \
	-p nsim_isa_core=6 \
	-on nsim_isa_sat
} elseif { [check_target_archs] } {
    lappend nsim_flags \
	-p nsim_isa_family=av2hs \
	-p nsim_isa_core=3 \
	-on nsim_isa_sat
    # Assume the SQRT is defined when we use DSP extensions
    if { [check_target_arc "__ARC_DSP__"] == 1 } {
	lappend nsim_flags \
	    -p nsim_isa_dsp_divsqrt_option=2 \
	    -p nsim_isa_dsp_wide_option=1
    }
} elseif { [check_target_arcem] } {
    lappend nsim_flags \
	-p nsim_isa_family=av2em \
	-p nsim_isa_core=3 \
	-on nsim_isa_sat
} else {
    perror "Unknown CPU configuration"
}

# Probe if GCC options require various hardware features.  For example, option
# -mll64 requires that nsim_isa_ll64_option=1 in nSIM properties. This is done
# via GCC C defines, where same -mll64, for example, sets __ARC_LL64__. Various
# -mcpu values also can have different features selected and different processor
# families can have common features, however many optional features of ARC EM
# are mandatory for ARC HS. Here, in DejaGNU board we are trying to avoid
# duplicating the logic of which cores has which options, so there would be no
# need to maintain those dependencies. Instead we simply probe for C defines and
# if they are set - add an nSIM option.
# Most nSIM options are disabled by default, therefore property should be passed
# to nSIM if C define is present. However a few options (bitscan and
# barrel-shifter in particular) are enabled by default, thus we should set an
# nSIM property that disable option if there is no C define. Second column in a
# table specifies that - if it is 1, then nSIM property is set only if C define
# is present and this property, presumably, enabled optional feature; but if
# value in second column is 0, then nSIM property is set only of C define is not
# present and, presuambly, that nSIM property disable optional feature.
set nsim_gcc_mapping {
    "__ARC64_ATOMIC_1__"	1	nsim_isa_atomic_option=1
    "__ARC64_ATOMIC_2__"	1	nsim_isa_atomic_option=2
    "__ARC64_ATOMIC_3__"	1	nsim_isa_atomic_option=3
    "__ARC64_M128__"		1	nsim_isa_m128_option=1
    "__ARC64_WIDE_VECTOR__"	1	nsim_isa_fp_wide_option=1
    "__ARC_ATOMIC__"		1	nsim_isa_atomic_option=1
    "__ARC_BARREL_SHIFTER__"	0	nsim_isa_shift_option=0
    "__ARC_CODE_DENSITY__"	1	nsim_isa_code_density_option=2
    "__ARC_DIVREM__"		1	nsim_isa_div_rem_option=2
    "__ARC_FPU_ASSIST__ "	1	nsim_isa_fpuda_option=1
    "__ARC_FPU_DP__"		1	nsim_isa_fpud_option=1
    "__ARC_FPU_DP_DIV__"	1	nsim_isa_fpud_div_option=1
    "__ARC_FPU_DP_FMA__"	1	nsim_isa_fpu_mac_option=1
    "__ARC_FPU_SP__"		1	nsim_isa_fpus_option=1
    "__ARC_FPU_SP_DIV__"	1	nsim_isa_fpus_div_option=1
    "__ARC_FPU_SP_FMA__"	1	nsim_isa_fpu_mac_option=1
    "__ARC_FPX_SP__"		1	nsim_isa_spfp=fast
    "__ARC_FPX_DP__"		1	nsim_isa_dpfp=fast
    "__ARC_LL64__"		1	nsim_isa_ll64_option=1
    "__ARC_MPY_WLH1__"		1	nsim_isa_mpy_option=2
    "__ARC_MPY_WLH2__"		1	nsim_isa_mpy_option=3
    "__ARC_MPY_WLH3__"		1	nsim_isa_mpy_option=4
    "__ARC_MPY_WLH4__"		1	nsim_isa_mpy_option=5
    "__ARC_MPY_WLH5__"		1	nsim_isa_mpy_option=6
    "__ARC_MPY_DMPY__"		1	nsim_isa_mpy_option=7
    "__ARC_MPY_MACD__"		1	nsim_isa_mpy_option=8
    "__ARC_MPY_QMACW__"		1	nsim_isa_mpy_option=9
    "__ARC_MULT32__"		1	nsim_isa_mult32=1
    "__ARC_MUL32BY16__"		1	nsim_isa_mul32x16=1
    "__ARC_NORM__"		0	nsim_isa_bitscan_option=0
    "__ARC_RF16__"		1	nsim_isa_rgf_num_regs=16
    "__ARC_DSP__"               1       nsim_isa_dsp_option=3
}

foreach {gcc_define set_if_defined nsim_prop} $nsim_gcc_mapping {
    if { [check_target_arc $gcc_define] == $set_if_defined } {
	lappend nsim_flags -p $nsim_prop
    }
}

# Allow user to specify additional options, like JIT, etc.
if { [info exists env(ARC_NSIM_OPTS) ] } {
    lappend nsim_flags $env(ARC_NSIM_OPTS)
}

# Setup nSIM.
set_board_info sim "time -p $nsim_bin [join $nsim_flags]"
# is_simulator should be set already by 'load_generic_config "sim"'. Do it here
# to be safe.
set_board_info is_simulator 1

# nsimdrv doesn't return application's exit code as it's own exit code, thus it
# is required to use status wrapper, that will print "***EXIT <number>" message.
set_board_info needs_status_wrapper 1


# Turn on support of compat.exp tests from GCC testsuite if environment
# variable ARC_GCC_COMPAT_SUITE == 1.
if { [info exists env(ARC_GCC_COMPAT_SUITE)] } {
    set_board_info arc,is_gcc_compat_suite $env(ARC_GCC_COMPAT_SUITE)
} else {
    if { ![board_info $board exists arc,is_gcc_compat_suite] } {
	set_board_info arc,is_gcc_compat_suite 0
    }
}

# We only support newlib on this target. We assume that all multilib
# options have been specified before we get here.
set_board_info compiler  "[find_gcc]"
set_board_info cflags    "[libgloss_include_flags] [newlib_include_flags] [arc_get_cflags]"
set_board_info ldflags   "[libgloss_link_flags] ${xldflags} [newlib_link_flags]"

# No linker script needed.
set_board_info ldscript ""

# Metaware hostlink can return values.
if { [board_info $board arc,hostlink] == "metaware" } {
    set_board_info gdb,noresults 0
} else {
    set_board_info gdb,noresults 1
}
# Doesn't pass arguments and signals, and doesn't do inferiorio.
set_board_info noargs 1
set_board_info gdb,nosignals 1
set_board_info gdb,noinferiorio 1

if [string is true -strict [board_info $board arc,is_gcc_compat_suite]] {
    fix_gcc_compat_multilib_flags
}

# vim: noexpandtab sts=4 ts=8:
